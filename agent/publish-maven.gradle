apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        owner.classpath += variant.javaCompileProvider.get().classpath
    }

    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "$group"
                artifactId = 'agent-espresso'

                artifact tasks.androidSourcesJar
                artifact tasks.androidJavadocsJar

                pom {
                    name = 'Zebrunner Agent Espresso (Java)'
                    description = 'Zebrunner Agent Espresso library'
                    url = 'https://github.com/zebrunner/java-agent-espresso'
                    organization {
                        name = 'Zebrunner'
                        url = 'https://zebrunner.com'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'dkazak'
                            name = 'Dmitry Kazak'
                            email = 'dkazak@solvd.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/zebrunner/java-agent-espresso.git'
                        developerConnection = 'git@github.com:zebrunner/java-agent-espresso.git'
                        url = 'https://github.com/zebrunner/java-agent-espresso'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/zebrunner/java-agent-espresso/issues'
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'MavenCentral'
                url = project.findProperty('publishing.maven.repo.url') ?: System.getenv('PUBLISHING_MAVEN_REPO_URL')
                credentials {
                    username = project.findProperty('publishing.maven.username') ?: System.getenv('PUBLISHING_MAVEN_USER')
                    password = project.findProperty('publishing.maven.password') ?: System.getenv('PUBLISHING_MAVEN_PASSWORD')
                }
            }
        }
    }
}

signing {
    def signingKey = project.findProperty('signing.key') ?: System.getenv('SIGNING_KEY')
    def signingPassword = project.findProperty('signing.password') ?: System.getenv('SIGNING_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}
